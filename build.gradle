plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.10"
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group

Properties additionalProperties = new Properties()
if (file("extra.properties").exists()) {
    additionalProperties.load(file("extra.properties").newReader())
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://masa.dy.fi/maven' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation("com.github.sakura-ryoko:malilib:${project.minecraft_version}-${project.malilib_version}")
    modCompileOnly "com.terraformersmc:modmenu:5.0.1"

    implementation "com.github.DarkKronicle.Konstruct:addons:${project.konstruct_version}"
    implementation "com.github.DarkKronicle.Konstruct:core:${project.konstruct_version}"

    implementation 'com.electronwill.night-config:toml:3.6.5'
    // Transitive
    include 'com.electronwill.night-config:toml:3.6.5'
    include 'com.electronwill.night-config:core:3.6.5'


    include "com.github.DarkKronicle.Konstruct:addons:${project.konstruct_version}"
    include "com.github.DarkKronicle.Konstruct:core:${project.konstruct_version}"

    implementation "org.mariuszgromada.math:MathParser.org-mXparser:${project.mxparser_version}"
    include "org.mariuszgromada.math:MathParser.org-mXparser:${project.mxparser_version}"

    implementation "io.github.maowimpl:owo:${project.owo_version}"
    include "io.github.maowimpl:owo:${project.owo_version}"

    implementation 'org.apache.commons:commons-csv:1.8'
    include 'org.apache.commons:commons-csv:1.8'

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

jar {
    from "LICENSE.txt"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'advancedchatcore'
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        //mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                // GitHub Packages requires a username + token; GH sets these in the runner
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
